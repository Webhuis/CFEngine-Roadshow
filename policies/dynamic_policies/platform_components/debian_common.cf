bundle agent debian_common {

vars:

 x86_64::
  "arch"                       string => "amd64";
 !x86_64::
  "arch"                       string => "${sys.arch}";

 any::

  "pkg[debian-goodies]"        string => "*";
  "pkg[openssh-server]"        string => "*";
  "pkg[libpam-ldap]"           string => "*";
  "pkg[libnss-ldap]"           string => "*";
  "pkg[libpam-cracklib]"       string => "*";
  "pkg[ldap-utils]"            string => "*";
  "pkg[smbclient]"             string => "*";

  "desired"                    string => "addupdate";
  "cfengine_version"           string => "3.6.6-1";
  "cfengine_package"           string => "cfengine-community-${cfengine_version}.${sys.arch}.deb";
  "package_cache"              string => "/var/cache/apt/archives";

  "cpkg[cfengine_community][package]"   string => "${package_cache}/${cfengine_package}";
  "cpkg[cfengine_community][desired]"   string => "${desired}";
  "cpkg[cfengine_community][version]"   string => "${cfengine_version}";
  "cpkg[cfengine_community][arch]"      string => "${sys.arch}";

methods:
 
 "Configure apt mirror"     usebundle => aptdeb_mirror;
 "Install common packages"  usebundle => packages("debian_common.pkg");
# "CFEngine custom package"  usebundle => get_custom_package("${cfengine_package}");
# "Update CFEngine"          usebundle => custom_packages("debian_common.cpkg");

reports:

 "${this.bundle}: CFEngine package string = ${package_cache}/${cfengine_package}";

}

bundle agent aptdeb_mirror {

vars:

  "mirror" string => "http://aptutl0001.webhuis.nl:3142/";

files:

  "/etc/apt/apt.conf"
          create  => "true",
        edit_line => edit_apt_mirror($(mirror)),
          classes => if_repaired("update_mirror");

commands:
 update_mirror::
  "/bin/rm /var/lib/apt/lists/lock";
  "/bin/rm /var/lib/dpkg/lock";
  "/usr/bin/apt-get update";
  "/usr/bin/apt-get upgrade -y";

reports:

 "${this.bundle}: ${mirror}";

}

bundle edit_line  edit_apt_mirror(string) {

delete_lines:
  ".*";

insert_lines:
  "Acquire::http::Proxy \"${string}\"${do_roadshow.punt_komma}";

}

bundle agent apt_repo (apt_repo) {

vars:

  "repo_index"          slist => getindices(${apt_repo});

methods:

  "Create apt repo" usebundle => create_apt_repo("${repo_index}", "$($(apt_repo)[$(repo_index)][deb])", "$($(apt_repo)[$(repo_index)][deb_src])", "$($(apt_repo)[$(repo_index)][repo_key])");

reports:

  "${this.bundle} apt repositories ${repo_index}";

}

bundle agent create_apt_repo (apt_repo, deb, deb_src, repo_key) {

files:

  "/etc/apt/sources.list.d/${apt_repo}.list"
          create  => "true",
        edit_line => edit_apt_repo(${apt_repo}, ${deb}, ${deb_src}),
          classes => if_repaired("new_apt_repo");

commands:

 new_apt_repo::
  "/usr/bin/apt-key adv --recv-keys --keyserver keys.gnupg.net ${repo_key}";
  "/usr/bin/apt-get update";

reports:

  "${this.bundle} apt repository ${apt_repo}, ${deb}, ${deb_src}";

}

bundle edit_line edit_apt_repo(apt_repo, deb, deb_src) {

delete_lines:

".*";

insert_lines:

"deb ${deb}";
"deb-src ${deb_src}";

}

####################################################

bundle agent packages (pkgs_array) {

vars:

  "package_name"         slist => getindices(${pkgs_array});

packages:

  "$(package_name)"
    package_policy             => "addupdate",
    package_method             => apt,
    package_version            => "$($(pkgs_array)[$(package_name)])";

reports:
 "${this.bundle}:  ${package_name}";

}

bundle agent custom_packages (cpkgs_array) {

vars:

  "custom_package_index" slist => getindices(${cpkgs_array});

  "package"             string => "$($(cpkgs_array)[$(custom_package_index)][package])";
  "desired"             string => "$($(cpkgs_array)[$(custom_package_index)][desired])";
  "version"             string => "$($(cpkgs_array)[$(custom_package_index)][version])";
  "arch"                string => "$($(cpkgs_array)[$(custom_package_index)][arch])";

methods:

  "Addupdate custom package" usebundle => custom_package_specific( "$(package)", "$(desired)", "$(version)", "$(arch)" );

packages:
 !any::
  "$(custom_package_index)"
    package_policy  => "addupdate",
    package_method  => dpkg_version("/var/cache/apt/archives"),
    package_select  => "==",
    package_version => "$($(cpkgs_array)[$(custom_package_index)])";

reports:

 "${this.bundle}: $(custom_package_index), $(package), $(desired), $(version), $(arch)";

}

bundle agent custom_package_specific(package, desired, version, arch) {

methods:

  "Custom package" usebundle => package_specific( "${package}", "${desired}", "${version}", "${arch}" );

reports:

  "${this.bundle}: $(package) $(desired), ${version}, ${arch}";

}

bundle agent get_custom_package(custom_package) {

files:

 "/var/cache/apt/archives/${custom_package}"
            comment => "Copy package",
             handle => "update_files_inputs_packages",
          copy_from => u_rcp("${do_roadshow.master_dynamic}/packages/${custom_package}","${sys.policy_hub}"),
             action => u_immediate;

reports:
 "${this.bundle}: ${custom_package}";

}

bundle agent nopackages (nopkgs_array) {

vars:
 any::
        "nopackage_name"            slist  => getindices("$(nopkgs_array)");

packages:

    any::

      "$(nopackage_name)"
            package_policy  => "delete",
            package_method  => apt,
            package_version => "$($(nopkgs_array)[$(nopackage_name)])";
}

bundle agent restart_sshd {

commands:
  "/usr/sbin/service sshd restart"
     handle => "restart_sshd",
    comment => "restarting sshd";

reports:
 "${this.bundle}";

}
