bundle agent debian_common {

vars:
    "pkg[debian-goodies]"      string => "*";
    "pkg[openssh-server]"      string => "*";

methods:
 
 "any" usebundle => packages("debian_common.pkg");
 "any" usebundle => aptdeb_mirror;
 "any" usebundle => os_common;

}

bundle agent aptdeb_mirror {

vars:
  "mirror" string => "http://aptutl01.webhuis.nl:3142/";

files:
  "/etc/apt/apt.conf"
          create  => "true",
        edit_line => edit_apt_mirror($(mirror)),
          classes => if_repaired("update_mirror");

commands:
 update_mirror::
  "/bin/rm /var/lib/apt/lists/lock";
  "/bin/rm /var/lib/dpkg/lock";
  "/usr/bin/apt-get update";
  "/usr/bin/apt-get upgrade -y";

}

####################################################

bundle agent packages (pkgs_array) {

vars:
 any::
        "package_name"            slist  => getindices("$(pkgs_array)");

packages:

    any::

      "$(package_name)"
            package_policy  => "addupdate",
            package_method  => apt,
            package_version => "$($(pkgs_array)[$(package_name)])";
}

bundle agent custom_packages (cpkgs_array) {

vars:
 any::
        "custom_package_name"            slist  => getindices("$(cpkgs_array)");

packages:

    debian::

      "$(custom_package_name)"
            package_policy  => "addupdate",
            package_method  => dpkg_version("/var/cache/apt/archives"),
            package_select  => "==",
            package_version => "$($(cpkgs_array)[$(custom_package_name)])";

}

bundle agent get_custom_package(custom_package) {

files:

 "/var/cache/apt/archives/${custom_package}"
  comment => "Copy package",
  handle => "update_files_inputs_packages",
  copy_from     => u_rcp("${master_dynamic}/packages/${custom_package}","${sys.policy_hub}"),
  action => u_immediate,
  classes => u_if_repaired("update_report");

}

bundle agent restart_sshd {

commands:
  "service ssh restart"
     handle => "restart_sshd",
    comment => "restarting sshd";

}
