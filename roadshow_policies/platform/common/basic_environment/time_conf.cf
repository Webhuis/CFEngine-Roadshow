bundle agent time_conf {

classes:

  "kvmo"                        expression => regcmp( "kvmo", $(classify.role) );

vars:

  "ntp_conf"                        string => "/etc/ntp.conf";
  "ntp_default"                     string => "/etc/default/ntpdate";
  "timezone"                        string => "GMT";
  "localtime"                       string => "/etc/localtime";
  "gmt"                             string => "/usr/share/zoneinfo/${timezone}";

  "pkg[ntp]"                        string => "*";
  "pkg[ntpdate]"                    string => "*";

  "nopkg[hobbit-plugins]"           string => "*";
  "nopkg[openntpd]"                 string => "*";

files:

  "${localtime}"
    comment             => "Set the right localtime (GMT), for once and forever.",
    action              => immediate,
    move_obstructions   => "true",
    link_from           => ln_s("${gmt}");

  "/etc/timezone"
    comment             => "Set the right timezone (GMT), for once and forever.",
    create              => "true",
    perms               => mo("644","root"),
    action              => r_immediate,
    edit_line           => edit_timezone(${timezone});

methods:

  "Time packages we need"        usebundle => packages("time_conf.pkg");
  "Time packages we do not need" usebundle => nopackages("time_conf.nopkg");

 !kvmo::

  "A VM is an ntp client"        usebundle => ntp_client;

 kvmo::

  "The host is a timeserver"     usebundle => ntp_server,
                                 comment   => "physical hosts service time, for the time being";

reports:

 "$(this.bundle): I am doing time configuration common to all platforms";

  

}
