bundle agent apt_cacher_ng {

vars:

  "pkg[apt-cacher-ng]"           string => "*";
  "templates"                     slist => { "acng.conf.tmpl", "apt_cacher_backends.tmpl" };
  "repo_index"                    slist => getindices("$($(class_domain).https_repo)");

methods:

  "We desire apt-cacher-ng"   usebundle => packages("apt_cacher_ng.pkg");
  "Get apt_cacher templates"  usebundle => get_template("$(templates)");
  "We desire https remap's"   usebundle => apt_cacher_remaps($(repo_index));
  "We desire https repo's"    usebundle => apt_cacher_backends($(repo_index));

reports:

  "${this.bundle}: $(repo_index)";

}

bundle agent apt_cacher_remaps (repo_index) {

vars:

  "acng_conf"                    string => "/etc/apt-cacher-ng/acng.conf";


files:

 "$(acng_conf)"
  comment       => "Edit acng.conf for https backends, Remap-repo: http://repo.nick ; file:backends_repo",
  create        => "true",
  action        => r_immediate,
  edit_template => "$(sys.workdir)/roadshow_dynamic_inputs/templates/acng.conf.tmpl",
  classes       => r_if_repaired("restart_apt_cacher");

reports:

  "${this.bundle}: $(repo_index)";

}

bundle agent apt_cacher_backends (repo_index) {

methods:

  "we desire backends files"  usebundle => apt_backend_file($(repo_index));

reports:

  "${this.bundle}: $(repo_index)";

}

bundle agent apt_backend_file(repo_index) {

vars:

  "apt_backends_file"            string => "/etc/apt-cacher-ng/backends_";

files:

 "$(apt_backends_file)$(repo_index)"
  comment       => "Create backends file containing the https repo names",
  create        => "true",
  action        => r_immediate,
  edit_template => "$(sys.workdir)/roadshow_dynamic_inputs/templates/apt_cacher_backends.tmpl",
  classes       => r_if_repaired("restart_apt_cacher");


reports:

  "${this.bundle}: $(repo_index)";

}
